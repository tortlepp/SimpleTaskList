apply plugin: "java"
apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "checkstyle"

// Use Java 8
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Repository for dependencies
repositories {
  mavenCentral()
}

// Legacy directory structure from Ant
sourceSets {
  main {
    java {
      srcDir "src"
    }
    resources {
      srcDirs = ["resources"]
    }
  }
}

// Configure the generated JAR archive
jar {
  manifest {
    attributes "Main-Class": "eu.ortlepp.tasklist.SimpleTaskList"
  }
  archiveName "SimpleTaskList.jar"
}

// Run the generated JAR file
task run(type: JavaExec) {
  description "Run the generated JAR file."
  classpath = jar.outputs.files
  main = "eu.ortlepp.tasklist.SimpleTaskList"
}

// Configure FindBugs
findbugs {
  ignoreFailures = true
  toolVersion = "3.0.1"
  sourceSets = [sourceSets.main]
  effort = "max"
  reportLevel = "low"
}

// Configure PMD
pmd {
  ignoreFailures = true
  toolVersion = "5.4.1"
  sourceSets = [sourceSets.main]
  ruleSets = ["java-basic", "java-braces", "java-codesize", "java-design", "java-empty", "java-imports", "java-naming", "java-optimizations", "java-strings", "java-sunsecure", "java-typeresolution", "java-unnecessary", "java-unusedcode"]
}

// Configure Checkstyle
checkstyle {
  ignoreFailures = true
  toolVersion = "6.14.1"
  configFile = new File(rootDir, "contrib/checkstyle_google.xml")
}
